#summary Character Controller using AS3.
#labels AS3,Examples,Character

= SEA3D Examples =

*[http://code.google.com/p/sea3d/downloads/detail?name=SEA3D%20AS3%20Examples%201.3.1.0.zip&can=2&q=#makechanges Download All Examples]*

Use keys to move. Press Space to kick.
*[http://www.sunag.com.br/sea3d/examples/?q=CharacterExample Preview]*

{{{
package
{
	import away3d.animators.AnimationStateBase;
	import away3d.animators.SkeletonAnimationSet;
	import away3d.animators.SkeletonAnimationState;
	import away3d.animators.SkeletonAnimator;
	import away3d.animators.nodes.AnimationNodeBase;
	import away3d.animators.transitions.CrossfadeStateTransition;
	import away3d.containers.Scene3D;
	import away3d.containers.View3D;
	import away3d.debug.AwayStats;
	import away3d.entities.Mesh;
	import away3d.events.AnimationStateEvent;
	import away3d.events.AnimatorEvent;
	import away3d.modifiers.MorphModifier;
	import away3d.morph.MorphNode;
	
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.KeyboardEvent;
	import flash.events.MouseEvent;
	import flash.ui.Keyboard;
	import flash.ui.KeyboardType;
	import flash.utils.getTimer;
	
	import sunag.animation.AnimationTarget;
	import sunag.events.SEAEvent;
	import sunag.sea3d.SEA3D;
	import sunag.utils.TimeStep;
	
	[SWF(width="1024", height="632", backgroundColor="0x2f3032", frameRate="60")]
	public class CharacterExample extends Sprite
	{
		private var view:View3D;
		private var scene:Scene3D;
		private var sea3d:SEA3D;		
		private var keyState:Array = [];
		private var timeStep:TimeStep = new TimeStep(stage.frameRate);
		
		[Embed (source="../assets/Character.sea",mimeType="application/octet-stream")]
		private var Character:Class;
		
		public function CharacterExample()
		{
			/**
			 * Basic config.
			 * */
			
			stage.stageFocusRect = false;
			stage.showDefaultContextMenu=false;	
			stage.align = StageAlign.TOP_LEFT;
			stage.scaleMode = StageScaleMode.NO_SCALE;
			
			scene = new Scene3D();			
			
			view = new View3D(scene);			
			view.backgroundColor = stage.color;
			view.antiAlias = 4;
			addChild(view);
			addChild(new AwayStats(view));
			
			/**
			 * Init loader for Away3D.
			 * */			
			
			sea3d = new SEA3D();
			
			sea3d.addEventListener(SEAEvent.COMPLETE, onComplete);			
			sea3d.loadBytes(new Character());	
			
			/**
			 * Interactive
			 * */
			
			stage.addEventListener(KeyboardEvent.KEY_DOWN, onKeyDown);
			stage.addEventListener(KeyboardEvent.KEY_UP, onKeyUp);
		}
		
		private function onKeyDown(e:KeyboardEvent):void
		{
			keyState[e.keyCode.toString()] = true;
		}
		
		private function onKeyUp(e:KeyboardEvent):void
		{
			delete keyState[e.keyCode.toString()];
		}
		
		private function getKeyState(code:int):Boolean
		{
			return keyState[code.toString()];
		}
		
		private function onEnterFrame(e:Event):void
		{
			var mesh:Mesh = sea3d.getMesh("Player");	
			var animator:SkeletonAnimator = sea3d.getSkeletonAnimator("Player");	
			
			var running:Boolean = false;
						
			animator.playbackSpeed = 1;
			
			if (animator.activeState.stateName == "pass#1")
			{
				
			}			
			else if (animator.activeState.stateName == "run" || animator.activeState.stateName == "idle")
			{								
				if (getKeyState(Keyboard.UP))
				{
					running = true;
					mesh.moveBackward(5 * timeStep.delta);
				}
				if (getKeyState(Keyboard.DOWN))
				{
					running = true;
					mesh.moveForward(5 * timeStep.delta);
					animator.playbackSpeed = -1;
				}
				
				if (getKeyState(Keyboard.LEFT))
				{
					mesh.rotationY -= 5 * timeStep.delta;
				}
				if (getKeyState(Keyboard.RIGHT))
				{
					mesh.rotationY += 5 * timeStep.delta;
				}	
				
				if (running)
				{
					if (animator.activeState.stateName != "run") 
					{
						animator.play("run", new CrossfadeStateTransition(.3));	
					}
				}
				else
				{
					if (animator.activeState.stateName != "idle") 
					{
						animator.play("idle", new CrossfadeStateTransition(.3));	
					}
				}
				
				if (getKeyState(Keyboard.SPACE))
				{
					animator.play("pass#1", new CrossfadeStateTransition(.3));
				}
			}
			
			// trace animation position
			//trace(AnimationNodeBase(animator.activeState.rootNode).position);
			
			//view.camera.lookAt(mesh.position);
			
			view.render();
		}
		
		private function onPassDone(e:AnimationStateEvent):void
		{			
			sea3d.getSkeletonAnimator("Player").play("idle", new CrossfadeStateTransition(.3));
		}
		
		private function onComplete(e:SEAEvent):void
		{		
			var animator:SkeletonAnimator = sea3d.getSkeletonAnimator("Player");
			
			animator.play("idle");
			
			// call function "pass#1" animation completed
			AnimationStateBase(animator.animationSet.getState("pass#1")).addEventListener(AnimationStateEvent.PLAYBACK_COMPLETE, onPassDone);			
						
			// List Sequences
			trace("<--");
			for(var i:int=0;i<animator.animationSet.states.length;i++)
				trace(animator.animationSet.states[i].stateName);
			trace("-->");
			
			/**
			 * <Camera001> Camera contained in MAX file.
			 * <bridge.get...> Using for get element.			 
			 * */						
			
			view.camera = sea3d.getCamera("Camera001");
			
			/**
			 * <bridge.container> contains all elements loaded.
			 * add objects in scene container
			 * */
			
			scene.addChild(sea3d.container);
			
			/**
			 * Start render and update.
			 * */
			
			stage.addEventListener(Event.ENTER_FRAME, onEnterFrame);
		}
	}
}
}}}
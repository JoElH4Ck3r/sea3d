#summary Multilayer Texture using AS3.
#labels Multilayer,Texture,Examples,AS3

= SEA3D Examples =

*[http://code.google.com/p/sea3d/downloads/detail?name=SEA3D%20AS3%20Examples%201.3.1.0.zip&can=2&q=#makechanges Download All Examples]*

Move mouse around screen.
*[http://www.sunag.com.br/sea3d/examples/?q=Multilayer Preview]*

{{{
package
{
	import away3d.containers.Scene3D;
	import away3d.containers.View3D;
	import away3d.debug.AwayStats;
	import away3d.entities.Mesh;
	import away3d.materials.TextureMaterial;
	import away3d.materials.methods.LayeredDiffuseMethod;
	import away3d.materials.methods.LayeredTexture;
	
	import flash.display.Sprite;
	import flash.display.StageAlign;
	import flash.display.StageScaleMode;
	import flash.events.Event;
	import flash.events.MouseEvent;
	
	import sunag.animation.AnimationTarget;
	import sunag.events.SEAEvent;
	import sunag.sea3d.SEA3D;
	
	[SWF(width="1024", height="632", backgroundColor="0x2f3032", frameRate="60")]
	public class Multilayer extends Sprite
	{
		private var view:View3D;
		private var scene:Scene3D;
		private var sea3d:SEA3D;
		
		[Embed (source="../assets/LayeredTexture-NoAnimated.sea",mimeType="application/octet-stream")]
		private var SimpleScene:Class;
		
		public function Multilayer()
		{
			/**
			 * Basic config.
			 * */
			
			stage.stageFocusRect = false;
			stage.showDefaultContextMenu=false;	
			stage.align = StageAlign.TOP_LEFT;
			stage.scaleMode = StageScaleMode.NO_SCALE;
			
			scene = new Scene3D();			
			
			view = new View3D(scene);			
			view.backgroundColor = stage.color;
			view.antiAlias = 4;
			addChild(view);
			addChild(new AwayStats(view));
						
			/**
			 * Init loader for Away3D.
			 * */			
			
			sea3d = new SEA3D();
						
			sea3d.addEventListener(SEAEvent.COMPLETE, onComplete);			
			sea3d.loadBytes(new SimpleScene());	
		}
		
		private function onEnterFrame(e:Event):void
		{		
			/**
			 * Interaction
			 * Cloud
			 * */
			
			var cloud:Mesh = sea3d.getMesh("Plane003");
			
			// (Multi-Sub Objects)
			var cloudMaterial:TextureMaterial = TextureMaterial(cloud.subMeshes[0].material);
			// Single Object
			//var cloudMaterial:TextureMaterial = TextureMaterial(cloud.material);
			
			var cloudDiffuse:LayeredDiffuseMethod = LayeredDiffuseMethod(cloudMaterial.diffuseMethod);			
						
			cloudDiffuse.layers[0].offsetU += .001;
			cloudDiffuse.layers[1].offsetU += .002;
				
			// Interative
			cloudDiffuse.layers[0].scaleU = .5 + (stage.mouseX / stage.stageWidth);
			cloudDiffuse.layers[0].scaleV = .5 + (stage.mouseY / stage.stageHeight);
			
			// Adding Layer Example "Not use here :)"
			//cloudDiffuse.addLayer(new LayeredTexture(yourTexture));
			
			/**
			 * Interaction
			 * BlendTexture
			 * */
			
			var plane:Mesh = sea3d.getMesh("Plane001");
			
			// (Multi-Sub Objects)
			var planeMaterial:TextureMaterial = TextureMaterial(plane.subMeshes[0].material);
			// Single Object
			//var planeMaterial:TextureMaterial = TextureMaterial(plane.material);
			
			var planeDiffuse:LayeredDiffuseMethod = LayeredDiffuseMethod(planeMaterial.diffuseMethod);			
			
			planeDiffuse.layers[0].scaleU += .001;
			planeDiffuse.layers[0].scaleV += .001;
			
			planeDiffuse.layers[1].offsetU -= .001;
			planeDiffuse.layers[1].offsetU -= .001;
			
			view.render();
		}
		
		private function onComplete(e:SEAEvent):void
		{
			/**
			 * <Camera001> Camera contained in MAX file.
			 * <bridge.get...> Using for get element.			 
			 * */						
			
			view.camera = sea3d.getCamera("Camera001");
			
			/**
			 * <bridge.container> contains all elements loaded.
			 * add objects in scene container
			 * */
			
			scene.addChild(sea3d.container);
			
			/**
			 * Start render and update.
			 * */
			
			stage.addEventListener(Event.ENTER_FRAME, onEnterFrame);
		}
	}
}
}}}